import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id "com.gradle.plugin-publish" version "0.12.0"
}

apply plugin: "java-gradle-plugin"
apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: "org.jetbrains.kotlin.kapt"
apply plugin: "com.vanniktech.maven.publish"

repositories {
    mavenCentral()
}

gradlePlugin {
    plugins {
        noCopyPlugin {
            id = "dev.ahmedmourad.nocopy.nocopy-gradle-plugin"
            implementationClass = "dev.ahmedmourad.nocopy.gradle.NoCopyGradlePlugin"
        }
    }
}

pluginBundle {
    website = "http://nocopy.ahmedmourad.dev/"
    vcsUrl = "https://github.com/AhmedMourad0/no-copy"
    description = "The Gradle plugin for no-copy, a Kotlin compiler plugin that performs `copy-erasure` on data classes."
    tags = ["kotlin-compiler", "gradle-plugin", "intellij-plugin", "compiler-plugin", "data-class", "value-based",
            "annotations", "kotlin-extensions", "kotlin", "kotlin-language", "kotlin-library", "kotlin-compiler-plugin",
            "inspections", "kotlin-plugin"]

    plugins {
        noCopyPlugin {
            displayName = "NoCopy Gradle Plugin"
            version = "1.3.0"
        }
    }

    mavenCoordinates {
        groupId = "dev.ahmedmourad.nocopy"
        artifactId = "nocopy-gradle-plugin"
        version = "1.3.0"
    }
}

tasks.withType(KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

jar {
    manifest {
        attributes["Specification-Title"] = project.name
        attributes["Specification-Version"] = project.version
        attributes["Implementation-Title"] = "dev.ahmedmourad.nocopy.nocopy-gradle-plugin"
        attributes["Implementation-Version"] = project.version
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-gradle-plugin-api:$kotlin_version"
    compileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

    implementation project(":core")
}
